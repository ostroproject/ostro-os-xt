require conf/distro/ostro.conf
require conf/distro/include/ostro-xt-security-flags.inc

DISTRO = "ostro-xt"
DISTRO_NAME = "Ostro OS XT"
DISTRO_FEATURES += " \
    x11 \
    opengl \
"

# disable IMA
DISTRO_FEATURES_remove = "ima"

SDK_VENDOR = "-ostroxtsdk"
SDK_NAME_PREFIX = "ostroxt"
SDK_UPDATE_URL = "${OSTRO_XT_SDK_UPDATE_URL}"

# SDK_UPDATE_URL defines the eSDK updateserver configured in
# devtool.conf when populate-sdk-ext is run.
#
# The default setting can be overridden in local.conf
OSTRO_XT_SDK_UPDATE_URL ??= "https://download.ostroproject.org/builds/ostro-os-xt/latest/sdk-data/${MACHINE}"

# Disable support for clang since meta-clang isn't really production ready
RDEPENDS_packagegroup-core-standalone-sdk-target_remove = "compiler-rt-dev"
RDEPENDS_packagegroup-core-standalone-sdk-target_remove = "compiler-rt-staticdev"
RDEPENDS_packagegroup-core-standalone-sdk-target_remove = "libcxx-dev"
RDEPENDS_packagegroup-core-standalone-sdk-target_remove = "libcxxabi-dev"
RDEPENDS_packagegroup-core-standalone-sdk-target_remove = "llvm-libunwind-dev"

# Fix CMake needed by clang
SSTATE_SCAN_FILES_append = " *.cmake"

# Tweak packages added by ROS
PREFERRED_VERSION_cv-bridge ?= "1.99.%"
PREFERRED_VERSION_image-geometry ?= "1.99.%"

PREFERRED_PROVIDER_virtual/opencl-headers ?= "beignet"
PREFERRED_PROVIDER_virtual/opencl-headers-cxx ?= "beignet"

# By default .la files are not removed from Yocto distros
INHERIT += "remove-libtool"

# Ostro OS XT adds some additional users and groups for which
# we need static IDs.
USERADD_UID_TABLES += "conf/distro/include/ostro-xt-passwd"
USERADD_GID_TABLES += "conf/distro/include/ostro-xt-group"

SUPPORTED_RECIPES_append = " ${META_OSTRO_XT_BASE}/conf/distro/include/ostro-xt-supported-recipes.txt"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_remove_intel-corei7-64 = " \
    spi-minnowmax-board \
    i2c-minnowmax-board \
"

# Settings for current BXT platforms. Normally this would go into a
# machine configuration, but Ostro OS XT only targets BXT, so this is
# okayish here.
APPEND += " console=ttyS2,115200 video=efifb maxcpus=4 noxsave reboot=efi kmemleak=off"
SERIAL_CONSOLE = "115200 ttyS2"
# We get console=ttyS0,115200 from meta-intel/conf/machine/intel-corei7-64.conf and
# need to get rid of it.
APPEND_remove = "console=ttyS0,115200"

# Mixing X11 startup from meta-oe (xserver-common) and OE-core
# (x11-common) fails (they conflict). xserver-nodm-init from meta-oe
# brings in xserver-common, so we stick to the one from OE-core.
BBMASK += "/meta-oe/recipes-graphics/xserver-nodm-init/"

# We need swig from OE-core, not meta-intel-iot-middleware
BBMASK += "/meta-intel-iot-middleware/recipes-devtools/swig/"

# Blacklisted in meta-oe because it fails to compile with gcc 6.
# But we can use it because we are still on gcc 5.x.
PNBLACKLIST[opencv] = ""
PNBLACKLIST[glmark2] = ""

# When undoing the opencv blacklisting, the older
# 2.x version also gets un-blacklisted although
# it isn't compatible with current ffmpeg, and
# because of DEFAULT_PREFERENCE = "-1" in the 3.1.bb
# we end up picking the 2.x version unless
# we explicitly request 3.x.
PREFERRED_VERSION_opencv = "3.%"

# Some recipes (like fwts) started to use compile
# flags that only work with gcc 6. Need to
# remove them.
CFLAGS_remove = "-Wno-error=misleading-indentation"
CXXFLAGS_remove = "-Wno-error=misleading-indentation"
