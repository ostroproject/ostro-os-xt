# We have a conf directory, add to BBPATH.
BBPATH =. "${LAYERDIR}:"

## Add our modified ros class to path
BBPATH .= ":${LAYERDIR}/recipes-ros"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend \
"

BBFILE_COLLECTIONS += "ostro-xt"
BBFILE_PATTERN_ostro-xt := "^${LAYERDIR}/"
BBFILE_PRIORITY_ostro-xt = "10"

# meta-ostro-xt/conf/*.sample files were copied from ostro-os/meta-ostro/conf.
# When Ostro OS changes those files, meta-ostro-xt needs to be updated
# accordingly:
# - merge the new upstream changes into the copied files (three-way merge)
# - bump the EXPECTED_OSTRO_OS_LOCALCONF/LAYER_CONF_VERSION version numbers here
#
# The Python code in the variable assignments below checks for that.
#
# When making additional changes to the copied files that are specific
# to Ostro OS XT, just bump OSTRO_OS_XT_LOCALCONF/LAYER_CONF_VERSION.
EXPECTED_OSTRO_OS_LOCALCONF_VERSION = "4"
EXPECTED_OSTRO_OS_LAYER_CONF_VERSION = "9"
OSTRO_OS_XT_LOCALCONF_VERSION = "1"
OSTRO_OS_XT_LAYER_CONF_VERSION = "3"

LOCALCONF_VERSION = "${@ str(int(${OSTRO_OS_LOCALCONF_VERSION}) * 100 + int(${OSTRO_OS_XT_LOCALCONF_VERSION})) if ${EXPECTED_OSTRO_OS_LOCALCONF_VERSION} == ${OSTRO_OS_LOCALCONF_VERSION} else bb.fatal('OSTRO_OS_LOCALCONF_VERSION = ${OSTRO_OS_LOCALCONF_VERSION} does not match EXPECTED_OSTRO_OS_LOCALCONF_VERSION = ${EXPECTED_OSTRO_OS_LOCALCONF_VERSION}, meta-ostro-xt needs to be updated (see ${META_OSTRO_XT_BASE}/conf/layer.conf)') }"
LAYER_CONF_VERSION = "${@ str(int(${OSTRO_OS_LAYER_CONF_VERSION}) * 100 + int(${OSTRO_OS_XT_LAYER_CONF_VERSION})) if ${EXPECTED_OSTRO_OS_LAYER_CONF_VERSION} == ${OSTRO_OS_LAYER_CONF_VERSION} else bb.fatal('OSTRO_OS_LAYER_CONF_VERSION = ${OSTRO_OS_LAYER_CONF_VERSION} does not match EXPECTED_OSTRO_OS_LAYER_CONF_VERSION = ${EXPECTED_OSTRO_OS_LAYER_CONF_VERSION}, meta-ostro-xt needs to be updated (see ${META_OSTRO_XT_BASE}/conf/layer.conf)') }"

# Point in error messages to the right files in meta-ostro-xt.
SANITY_LOCALCONF_SAMPLE = "${META_OSTRO_XT_BASE}/conf/local.conf.sample"
SANITY_BBLAYERCONF_SAMPLE = "${META_OSTRO_XT_BASE}/conf/bblayers.conf.sample"

# Set a variable to get to the top of the Ostro OS XT metadata location.
META_OSTRO_XT_BASE := '${LAYERDIR}'
